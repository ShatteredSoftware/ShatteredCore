import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        maven {
          url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:2.2.+'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'java'
apply plugin: 'nebula-aggregate-javadocs'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'com.github.shatteredsuite'
version '1.0.0'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        name = "PaperMC"
        url = "https://papermc.io/repo/repository/maven-public/"
    }
    maven {
        name = "Jitpack"
        url = 'https://jitpack.io'
    }
    maven {
        name = "Spigot"
        url = "https://hub.spigotmc.org/nexus/content/groups/public"
    }
    maven {
        name = "CodeMC-Repo"
        url = 'http://repo.codemc.io/repository/maven-public'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/shatteredsuite/shatteredcore")
            credentials {
                username = project.findProject("gpr.user") ?: System.getenv("USERNAME")
                username = project.findProject("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            artifactId = 'core-all'
            project.shadow.component(publication)
        }
        maven(MavenPublication) {
            artifactId = 'core'
            from components.java
        }
    }
}

task getHash {
    def p1 = 'git rev-parse --short HEAD'.execute()
    p1.waitFor()
    getHash.ext.hash = p1.text
}

processResources {
    dependsOn getHash
    def hash = getHash.hash
    with copySpec{
        from 'src/main/resources'
        filter(ReplaceTokens, tokens: [version: project.version + ' (' + hash.trim() + ')'])
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.15.1-R0.1-SNAPSHOT'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "fr.minuskube.inv:smart-invs:1.2.7"
    implementation "de.tr7zw:item-nbt-api:2.3.1"
    implementation "org.bstats:bstats-bukkit:1.7"
    implementation "com.github.cryptomorin:XSeries:5.3.1"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    dependencies {
        exclude(dependency('com.destorystokyo:paper-api:.*'))
    }
    relocate 'fr.minuskube.inv', 'com.github.shatteredsuite.core.include.inv'
    relocate 'de.tr7zw.changeme.nbtapi', 'com.github.shatteredsuite.core.include.nbt'
    relocate 'org.bstats.bukkit', 'com.github.shatteredsuite.core.include.stats'
    relocate 'com.cryptomorin.xseries', 'com.github.shatteredsuite.core.include.xseries'
    classifier = null
}
