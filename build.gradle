import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    ext.kotlin_version = '1.3.72'
    ext.xseries_version = project.property('xSeriesVersion')
    ext.nbt_version = project.property('nbtApiVersion')
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:2.2.+'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'jacoco'
    id 'kr.entree.spigradle' version '2.2.3'
}

apply plugin: 'java'
apply plugin: 'nebula-aggregate-javadocs'
apply plugin: 'maven-publish'

group = project.property('pluginGroup')
version = project.property('version')

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    papermc()
    jitpack()
    spigotmc()
    codemc()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

task getHash {
    def p1 = 'git rev-parse --short HEAD'.execute()
    p1.waitFor()
    getHash.ext.hash = p1.text
}

processResources {
    dependsOn getHash
    def hash = getHash.hash
    with copySpec {
        from 'src/main/resources'
        filter(ReplaceTokens, tokens: [description: project.property('description'),
                                       projectName: project.property('projectName'),
                                       pluginName : project.property('pluginName'),
                                       version    : project.version + ' (' + hash.trim() + ')'])
    }
}

configurations {
    implementation {
        canBeResolved true
    }
}

dependencies {
    def withoutSpigot = { exclude group: "org.spigotmc", module: "spigot-api" }
    implementation spigot('1.15.2')
    implementation bStats()
    compile 'org.jetbrains:annotations:16.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation group: "com.google.code.gson", name: "gson", version: project.property("gsonVersion")
    implementation group: "fr.minuskube.inv", name: "smart-invs", version: project.property("smartInvsVersion"), withoutSpigot
    implementation group: "de.tr7zw", name: "item-nbt-api", version: project.property('nbtApiVersion')
    implementation group: "com.github.cryptomorin", name: "XSeries", version: project.property('xSeriesVersion')
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.4.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.21.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    from sourceSets.main.output
    baseName = 'ShatteredCore'
    classifier = null
    configurations = [project.configurations.implementation]
    dependencies {
        include(dependency("org.jetbrains.kotlin:.*:.*"))
        include(dependency("fr.minuskube.inv:smart-invs:1.2.7"))
        include(dependency("de.tr7zw:item-nbt-api:$nbt_version"))
        include(dependency("org.bstats:bstats-bukkit:1.7"))
        include(dependency("com.github.cryptomorin:XSeries:$xseries_version"))
        include(dependency("com.google.code.gson:gson:.*"))
    }
    relocate 'fr.minuskube.inv', 'com.github.shatteredsuite.core.include.inv'
    relocate 'de.tr7zw.changeme.nbtapi', 'com.github.shatteredsuite.core.include.nbt'
    relocate 'org.bstats.bukkit', 'com.github.shatteredsuite.core.include.stats'
    relocate 'com.cryptomorin.xseries', 'com.github.shatteredsuite.core.include.xseries'
}

tasks.generateSpigotDescription.enabled = false

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled true
    }
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/shatteredsuite/shatteredcore")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId 'core'
            artifact javadocJar
            artifact sourcesJar
            artifact shadowJar
            pom {
                name = project.property("projectName")
                description = project.property('description')
                url = 'https://github.com/ShatteredSuite/' + project.property("pluginName")
                developers {
                    developer {
                        id = 'UberPilot'
                        name = 'UberPilot'
                        email = 'uberpilot99@gmail.com'
                    }
                }
                withXml {
                    def depsNode = asNode().appendNode('dependencies')

                    configurations.compile.allDependencies.each {
                        def depNode = depsNode.appendNode('dependency')
                        depNode.appendNode('groupId', it.group)
                        depNode.appendNode('artifactId', it.name)
                        depNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
