import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    ext.kotlin_version = project.property('kotlinVersion')
    ext.xseries_version = project.property('xSeriesVersion')
    ext.nbt_version = project.property('nbtApiVersion')
    ext.gsonVersion = project.property('gsonVersion')
    ext.jacksonVersion = project.property('jacksonVersion')
    ext.h2Version = project.property('h2Version')
    ext.hikariVersion = project.property('hikariVersion')
    ext.adventureApiVersion = project.property('adventureApiVersion')
    ext.adventureBukkitVersion = project.property('adventureBukkitVersion')
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:2.2.+'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.5.30"
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'jacoco'
}

apply plugin: 'java'
apply plugin: 'nebula-aggregate-javadocs'
apply plugin: 'maven-publish'

group = project.property('pluginGroup')
version = project.property('version')

repositories {
    mavenLocal()
    maven {
        url 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven {
        url = "https://repo.codemc.io/repository/maven-public/"
    }
    mavenCentral()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url = 'https://oss.sonatype.org/content/repositories/central'
    }
    maven {
        url = "https://repo.codemc.io/repository/nms/"
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven {
        url = 'https://jitpack.io'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
    withJavadocJar()
    withSourcesJar()
}

task getHash {
    def p1 = 'git rev-parse --short HEAD'.execute()
    p1.waitFor()
    getHash.ext.hash = p1.text
}


processResources {
    dependsOn getHash
    def hash = getHash.hash
    with copySpec {
        from 'src/main/resources'
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        filter(ReplaceTokens, tokens: [description   : project.property('description'),
                                       projectName   : project.property('projectName'),
                                       pluginName    : project.property('pluginName'),
                                       website       : project.property('website'),
                                       apiVersion    : project.property('apiVersion'),
                                       version       : project.version + ' (' + hash.trim() + ')',
                                       kotlinVersion : project.property('kotlinVersion'),
                                       gsonVersion   : project.property('gsonVersion'),
                                       jacksonVersion: project.property('jacksonVersion'),
                                       h2Version     : project.property('h2Version'),
                                       hikariVersion : project.property('hikariVersion')])
    }
}

configurations {
    implementation {
        canBeResolved true
    }
}

registerCustomOutputTask("hhenrichsen", "C://Users/Hunter/Documents/Servers/TestServer/plugins", name)

def registerCustomOutputTask(name, path, plugin) {
    tasks.register('build' + name, Copy) {
        group('development')
        outputs.upToDateWhen { false }
        dependsOn ':jars'
        from(file('build/libs/' + plugin + '-' + version + '.jar'))
        into(file(path))
        rename { String fileName ->
            fileName.replace(plugin + '-' + version + '-all.jar', plugin + ".jar")
        }
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:16.0.2'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation group: "com.google.code.gson", name: "gson", version: gsonVersion
    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"

    implementation "com.zaxxer:HikariCP:$hikariVersion"
    implementation "com.h2database:h2:$h2Version"

    def withoutSpigot = { exclude group: "org.spigotmc", module: "spigot-api" }

    implementation group: "io.papermc.paper", name: "paper-api", version: project.property("paperVersion")
    implementation group: "net.kyori", name: "adventure-text-minimessage", version: "4.1.0-SNAPSHOT"
    implementation group: "net.kyori", name: "adventure-api", version: project.property("adventureApiVersion")
    implementation group: "net.kyori", name: "adventure-platform-bukkit", version: project.property("adventureBukkitVersion")
    implementation group: "org.bstats", name: "bstats-bukkit", version: "2.2.1"
    implementation group: "fr.minuskube.inv", name: "smart-invs", version: project.property("smartInvsVersion"), withoutSpigot
    implementation group: "de.tr7zw", name: "item-nbt-api", version: project.property("nbtApiVersion")
    implementation group: "com.github.cryptomorin", name: "XSeries", version: project.property("xSeriesVersion")
    implementation group: "com.github.MilkBowl", name: "VaultAPI", version: "1.7"

    // Testing
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.4.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.11.2'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_16
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_16
}


task largeJar(type: ShadowJar) {
    from sourceSets.main.output
    baseName = 'ShatteredCore'
    classifier = 'large'
    configurations = [project.configurations.implementation]
    dependencies {
        include(dependency("com.google.code.gson:gson:$gsonVersion"))
        include(dependency("com.fasterxml.jackson.core:jackson-core:$jacksonVersion"))
        include(dependency("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"))
        include(dependency("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"))
        include(dependency("com.h2database:h2:$h2Version"))
        include(dependency("com.zaxxer:HikariCP:$hikariVersion"))
        include(dependency("org.jetbrains.kotlin:.*:.*"))
        include(dependency("net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT"))
        include(dependency("net.kyori:adventure-api:$adventureApiVersion"))
        include(dependency("net.kyori:adventure-platform-bukkit:$adventureBukkitVersion"))
        include(dependency("fr.minuskube.inv:smart-invs:$smartInvsVersion"))
        include(dependency("de.tr7zw:item-nbt-api:$nbt_version"))
        include(dependency("org.bstats:bstats-bukkit:1.7"))
        include(dependency("com.github.cryptomorin:XSeries:$xseries_version"))
    }
    minimize {
        exclude(dependency("com.google.code.gson:gson:$gsonVersion"))
        exclude(dependency("com.fasterxml.jackson.core:jackson-core:$jacksonVersion"))
        exclude(dependency("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"))
        exclude(dependency("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"))
        exclude(dependency("com.h2database:h2:$h2Version"))
        exclude(dependency("com.zaxxer:HikariCP:$hikariVersion"))
        exclude(dependency("org.jetbrains.kotlin:.*:.*"))
        exclude(dependency("net.kyori:adventure-api:$adventureApiVersion"))
        exclude(dependency("net.kyori:adventure-platform-bukkit:$adventureBukkitVersion"))
        exclude(dependency("fr.minuskube.inv:smart-invs:$smartInvsVersion"))
        exclude(dependency("de.tr7zw:item-nbt-api:$nbt_version"))
        exclude(dependency("org.bstats:bstats-bukkit:1.7"))
        exclude(dependency("com.github.cryptomorin:XSeries:$xseries_version"))
    }
    relocate 'fr.minuskube.inv', 'com.github.shatteredsuite.core.include.inv'
    relocate 'de.tr7zw.changeme.nbtapi', 'com.github.shatteredsuite.core.include.nbt'
    relocate 'org.bstats.bukkit', 'com.github.shatteredsuite.core.include.stats'
    relocate 'com.cryptomorin.xseries', 'com.github.shatteredsuite.core.include.xseries'
}

task jars {
    dependsOn shadowJar, javadocJar, sourcesJar, largeJar
}

shadowJar {
    from sourceSets.main.output
    baseName = 'ShatteredCore'
    classifier = null
    configurations = [project.configurations.implementation]
    dependencies {
        include(dependency("net.kyori:adventure-api:$adventureApiVersion"))
        include(dependency("net.kyori:adventure-platform-bukkit:$adventureBukkitVersion"))
        include(dependency("fr.minuskube.inv:smart-invs:$smartInvsVersion"))
        include(dependency("de.tr7zw:item-nbt-api:$nbt_version"))
        include(dependency("org.bstats:bstats-bukkit:1.7"))
        include(dependency("com.github.cryptomorin:XSeries:$xseries_version"))
    }
    minimize {
        exclude(dependency("net.kyori:adventure-api:$adventureApiVersion"))
        exclude(dependency("net.kyori:adventure-platform-bukkit:$adventureBukkitVersion"))
        exclude(dependency("fr.minuskube.inv:smart-invs:$smartInvsVersion"))
        exclude(dependency("de.tr7zw:item-nbt-api:$nbt_version"))
        exclude(dependency("org.bstats:bstats-bukkit:1.7"))
        exclude(dependency("com.github.cryptomorin:XSeries:$xseries_version"))
    }
    relocate 'fr.minuskube.inv', 'com.github.shatteredsuite.core.include.inv'
    relocate 'de.tr7zw.changeme.nbtapi', 'com.github.shatteredsuite.core.include.nbt'
    relocate 'org.bstats.bukkit', 'com.github.shatteredsuite.core.include.stats'
    relocate 'com.cryptomorin.xseries', 'com.github.shatteredsuite.core.include.xseries'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled true
    }
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/shatteredsuite/shatteredcore")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId 'ShatteredCore'
            artifact javadocJar
            artifact sourcesJar
            artifact shadowJar
            pom {
                name = project.property("projectName")
                description = project.property('description')
                url = 'https://github.com/ShatteredSuite/' + project.property("pluginName")
                developers {
                    developer {
                        id = 'UberPilot'
                        name = 'UberPilot'
                        email = 'uberpilot99@gmail.com'
                    }
                }
                withXml {
                    def depsNode = asNode().appendNode('dependencies')

                    configurations.implementation.allDependencies.each {
                        def depNode = depsNode.appendNode('dependency')
                        depNode.appendNode('groupId', it.group)
                        depNode.appendNode('artifactId', it.name)
                        depNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
